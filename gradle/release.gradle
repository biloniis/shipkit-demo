//Plugin jars are added to the buildscript classpath in the root build.gradle file
apply plugin: "org.shipkit.shipkit-auto-version"

apply plugin: "org.shipkit.shipkit-changelog"
tasks.named("generateChangelog") {
    previousRevision = project.ext.'shipkit-auto-version.previous-tag'
    githubToken = System.getenv("GH_WRITE_TOKEN")
    repository = "shipkit/shipkit-demo"
}

apply plugin: "org.shipkit.shipkit-github-release"
tasks.named("githubRelease") {
    dependsOn tasks.named("generateChangelog")
    repository = "shipkit/shipkit-demo"
    changelog = tasks.named("generateChangelog").get().outputFile
    githubToken = System.getenv("GH_WRITE_TOKEN")
    newTagRevision = System.getenv("TRAVIS_COMMIT")
}

apply plugin: "io.github.gradle-nexus.publish-plugin" //https://github.com/gradle-nexus/publish-plugin/
nexusPublishing {
    repositories {
        if (System.getenv("SONATYPE_PASSWORD")) {
            sonatype {
                username = System.getenv("SONATYPE_USER")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

allprojects { p ->
    plugins.withId("java") {
        p.apply from: "$rootDir/gradle/java-publication.gradle"
    }
}
