//Auxiliary jar files required by Maven module publications
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//Gradle Maven publishing plugin configuration (https://docs.gradle.org/current/userguide/publishing_maven.html)
apply plugin: "maven-publish"
publishing {
    publications {
        maven(MavenPublication) { //name of the publication
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                description = "Demo project showcasing automated publications with Shipkit Gradle plugins"
                url = "https://github.com/mockito/shipkit-demo"
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/shipkit/shipkit-demo/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'mockitoguy'
                        name = 'Szczepan Faber'
                        url = 'https://github.com/mockitoguy'
                    }
                }
                scm {
                    url = 'https://github.com/shipkit/shipkit-demo.git'
                }
            }
        }
    }

    //useful for testing - running "publish" will create artifacts/pom in a local dir
    repositories { maven { url = "$buildDir/repo" } }
}

//Bintray Gradle plugin configuration (https://github.com/bintray/gradle-bintray-plugin)
apply plugin: "com.jfrog.bintray"

bintray {
    user = 'szczepiq'
    key = System.getenv('BINTRAY_API_KEY')
//    publish = true
    dryRun = project.hasProperty("dryRun")
    publications = ['maven']

    pkg {
        repo = 'examples'
        userOrg = 'shipkit'
        name = "basic"
        licenses = ['MIT']
        labels = ['continuous delivery', 'release automation', 'mockito', 'shipkit']
        vcsUrl = "https://github.com/shipkit/shipkit-demo.git"

        version {
            name = project.version
            vcsTag = "v$name"
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
